#!/usr/local/bin/perl

=head1 NAME

export-netspoc - Export data from Netspoc for use in Netspoc-Web

=head1 COPYRIGHT AND DISCLAIMER

(c) 2016 by Heinz Knutzen <heinz.knutzen@gmail.com>
(c) 2014 by Daniel Brunkhorst <daniel.brunkhorst@web.de>

https://github.com/hknutzen/Netspoc-Web

This program is free software: you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.
This program is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
GNU General Public License for more details.
You should have received a copy of the GNU General Public License
along with this program. If not, see <http://www.gnu.org/licenses/>.

=cut

use strict;
use warnings;
use Getopt::Long;
use File::Path 'make_path';
use JSON;
use Digest::SHA qw(sha1_base64);
use Netspoc::Compiler::GetArgs qw(read_config combine_config);
use Netspoc::Compiler::Common;
use Netspoc::Compiler::Pass1;
use open qw(:std :utf8);

sub usage {
    die "Usage: $0 [-q] netspoc-data out-directory\n";
}
my $quiet;

# Argument processing.
GetOptions ('quiet!' => \$quiet) or usage();
my $netspoc_data = shift @ARGV or usage();
my $out_dir = shift @ARGV or usage();

# Remove trailing slash.
$out_dir =~ s,/$,,;

# Copy version information from this file and
# take modification date for all newly created files.
my $policy_file = "$netspoc_data/POLICY";

sub abort {
    my ($msg) = @_;
    die "$msg\n";
}

sub create_dirs {
    my ($path) = @_;
    $path = "$out_dir/$path";
    make_path($path, {error => \my $err} );
    if (@$err) {
        for my $diag (@$err) {
            my ($file, $message) = %$diag;
            if ($file eq '') {
                die "General error: $message\n";
            }
            else {
                die "Problem creating $file: $message\n";
            }
        }
    }
    return;
}

sub export {
    my ($path, $data) = @_;
    $path = "$out_dir/$path";
    open (my $fh, '>', $path) or die "Can't open $path\n";
    print $fh to_json($data, {pretty => 1, canonical => 1});
    close $fh or die "Can't close $path\n";
    return;
}

sub by_name { return($a->{name} cmp $b->{name}) }

# Take higher bits from network NAT, lower bits from original IP.
# This works with and without NAT.
sub nat {
    my ($ip, $network) = @_;
    return($network->{ip} | $ip & complement_32bit ($network->{mask}));
}

sub ip_nat_for_object {
    my ($obj) = @_;
    my $ip;
    my $nat;

# This code is a modified copy of address.
# - It needs to handle objects of type 'Host' instead of 'Subnet'.
# - Handles dynamic NAT for hosts.
# - It returns strings of textual ip/mask, not pairs of numbers.
    my $type = ref $obj;
    if ($type eq 'Network') {
        my $get_ip = sub {
            my ($obj) = @_;
            if ($obj->{hidden}) {
                'hidden';
            }
            elsif ($obj->{ip} eq 'unnumbered') {
                $obj->{ip}
            }

            # Don't print mask for loopback network. It needs to have
            # exactly the same address as the corresponding loopback interface.
            elsif ($obj->{loopback}) {
                print_ip($obj->{ip});
            }

            # Print no mask for aggregate with mask 0, for compatibility
            # with old version.
            elsif ($obj->{is_aggregate} && $obj->{mask} == 0) {
                print_ip($obj->{ip});
            }
            else {
                join('/', print_ip($obj->{ip}), print_ip($obj->{mask}));
            }
        };
        $ip = $get_ip->($obj);
        if (my $hash = $obj->{nat}) {
            for my $tag (keys %$hash) {
                my $nat_obj = $hash->{$tag};
                $nat->{$tag} = $get_ip->($nat_obj);
            }
        }
    }
    elsif ($type eq 'Host') {
        my $get_ip = sub {
            my ($obj, $network) = @_;
            if ($network->{dynamic}) {
                my $nat_tag = $network->{nat_tag};
                if ($obj->{nat} and (my $ip = $obj->{nat}->{$nat_tag})) {

                    # Single static NAT IP for this host.
                    print_ip($ip);
                }
                elsif ($network->{hidden}) {
                    'hidden';
                }
                else {

                    # Dynamic NAT, take whole network.
                    join('/', 
                         print_ip($network->{ip}), print_ip($network->{mask}));
                }
            }
            else {
                if ( my $range = $obj->{range} ) {
                    join('-', map { print_ip(nat($_, $network)) } @$range);
                }
                else {
                    print_ip(nat($obj->{ip}, $network));
                }
            }
        };
        my $network = $obj->{network};
        $ip = $get_ip->($obj, $network);
        if (my $hash = $network->{nat}) {
            for my $tag (keys %$hash) {
                my $nat_obj = $hash->{$tag};
                $nat->{$tag} = $get_ip->($obj, $nat_obj);
            }
        }
    }
    elsif ($type eq 'Interface') {
        my $get_ip = sub {
            my ($obj, $network) = @_;
            if ($network->{dynamic}) {
                my $nat_tag = $network->{nat_tag};
                if (my $ip = $obj->{nat}->{$nat_tag}) {

                    # Single static NAT IP for this interface.
                    print_ip($ip);
                }
                elsif ($network->{hidden}) {
                    'hidden';
                }
                else {
                    
                    # Dynamic NAT, take whole network.
                    join('/', 
                         print_ip($network->{ip}), print_ip($network->{mask}));
                }
            }
            elsif ($obj->{ip} =~ /unnumbered|short|bridged/) {
                $obj->{ip};
            }
            elsif ($obj->{ip} eq 'negotiated') {

                # Take whole network.
                join('/', 
                     print_ip($network->{ip}), print_ip($network->{mask}));
            }
            else {
                print_ip(nat($obj->{ip}, $network));
            }
        };
        my $network = $obj->{network};
        $ip = $get_ip->($obj, $network);
        if (my $hash = $network->{nat}) {
            for my $tag (keys %$hash) {
                my $nat_obj = $hash->{$tag};
                $nat->{$tag} = $get_ip->($obj, $nat_obj);
            }
        }
    }
    else {
        internal_err "Unexpected object $obj->{name}";
    }
    return $nat ? ( ip => $ip, nat => $nat ) : ( ip => $ip );
}

######################################################################
# Setup zones
######################################################################

# We can't use %aggregates from Netspoc.pm because it only holds named
# aggregates. But we need unnamed aggregates like any:[network:XX]
# as well.
my @all_zones;

sub setup_zones {
    progress('Setup zones');
    my %seen;
    for my $network (values %networks) {
        $network->{disabled} and next;
        my $zone = $network->{zone};
        next if $seen{$zone}++;
#        debug "$network->{name} in $zone->{name}";
        push @all_zones, $zone;
    }
    return;
}

######################################################################
# Setup services
######################################################################

# Globally change name of owners from "owner:name" to "name".
sub adapt_owner_names {
    for my $owner (values %owners) {
        $owner->{name} =~ s/^owner://;
    }
}

sub owner_for_object {  
    my ($object) = @_;
    if (my $owner_obj = $object->{owner}) {
        return $owner_obj->{name};
    }
    return ();
}

sub part_owners_for_object {    
    my ($object) = @_;
    if (my $aref = $object->{part_owners}) {
        return map { $_->{name} } @$aref;
    }
    return ();
}

sub extended_owners_for_object {
    my ($object) = @_;
    my @result;
    if (my $owner_obj = $object->{extended_owner}) {
        @result = ($owner_obj);
        if (my $list = $owner_obj->{extended_by}) {
            push(@result, grep { $_->{extend_only} } @$list);
        }
    }
    return map { $_->{name} } @result;
}

sub owners_for_objects {        
    my ($objects) = @_;
    my %owners;
    for my $object (@$objects) {
        if (my $name = owner_for_object($object)) {
            $owners{$name} = $name;
        }
    }
    return [ sort values %owners ];
}

sub part_owners_for_objects {   
    my ($objects) = @_;
    my %owners;
    for my $object (@$objects) {
        for my $name (part_owners_for_object($object)) {
            $owners{$name} = $name;
        }
    }
    return [ sort values %owners ];
}

sub extended_owners_for_objects {   
    my ($objects) = @_;
    my %owners;
    for my $object (@$objects) {
        for my $name (extended_owners_for_object($object)) {
            $owners{$name} = $name;
        }
    }
    return [ sort values %owners ];
}

sub proto_descr {
    my ($protocols) = @_;
    my @result;
    for my $proto0 (@$protocols) {
        my $protocol = $proto0;
        my $desc = my $ptype = $protocol->{proto};
        my $num;
        if ($ptype eq 'tcp' or $ptype eq 'udp') {
            my $port_code = sub {
                my ($range_obj) = @_;
                my ($v1, $v2) = @{ $range_obj->{range} };
                if ($v1 == $v2) {
                    return $v1;
                }
                elsif ($v1 == 1 and $v2 == 65535) {
                    return (undef);
                }
                else {
                    return "$v1-$v2";
                }
            };
            my $src_range = $protocol->{src_range};
            my $sport = $src_range && $port_code->($src_range);
            my $dport = $port_code->($protocol->{dst_range});
            if ($sport) {
                $desc .= " $sport:$dport";
            }
            elsif ($dport) {
                $desc .= " $dport";
                ($num) = split('-', $dport)
            }
        }
        elsif ($ptype eq 'icmp') {
            if (defined(my $type = $protocol->{type})) {
                if (defined(my $code = $protocol->{code})) {
                    $desc .= " $type/$code";
                }
                else {
                    $desc .= " $type";
                }
                $num = $type;
            }
        }
        if (my $modifiers = $protocol->{modifiers}) {
            for my $key (sort keys %$modifiers) {
                next if $key eq 'stateless_icmp';
                next if $key eq 'overlaps';
                next if $key eq 'no_check_supernet_rules';
                $desc .= ", $key";
            }
        }
        $num ||= 0;
        push @result, [ $desc, $ptype, $num ];
    }
    @result = 
        map { $_->[0] }

        # Sort by protocol, port/type, all (if proto and num are equal)
        sort { $a->[1] cmp $b->[1] || 
               $a->[2] <=> $b->[2] || 
               $a->[0] cmp $b->[0] }
        @result;
    return \@result;
}

sub find_visibility {
    my ($owners, $uowners) = @_;
    my $visibility;
    my %hash = map { $_ => 1} @$owners;
    my @extra_uowners = grep { not $hash{$_} } @$uowners;
    my @DA_extra = grep({ $_ =~ /^DA_/ } @extra_uowners);
    my @other_extra = grep({ $_ !~ /^DA_/ } @extra_uowners);
                           
    # No known owner or owner of users.
    if (not @$owners and not @$uowners) {
        # Default: private
    }
    # Set of uowners is subset of owners.
    elsif (not @extra_uowners) {
        # Default: private
    }
    # Restricted visibility
    elsif (@other_extra <= 2) {
        if (@DA_extra >= 3) {
            $visibility = 'DA_*';
        }
    }
    else {
        $visibility = '*';
    }
    return $visibility;
}

# All objects referenced in rules and in networks and hosts of owners.
my %all_objects;

# Copy of %services with those services split, that have different 'user'.
my @normalized_services;

# Split service, if 'user' has different values in normalized rules.
sub normalize_services_for_export {
    progress("Normalize services for export");
 
    for my $service (sort by_name values %services) {
        next if $service->{disabled};
        my $sname   = $service->{name};
        my $context = $sname;
        my $user    = expand_group($service->{user}, "user of $context");
        my $rules   = $service->{rules};
        my $foreach = $service->{foreach};

        my %key2rules;
        my %key2user;

        my $seen_as_user = sub {
            my ($list) = @_;
            my $list_key = join(',', map { $_->{name} } @$list);
            return $key2user{$list_key};
        };

        for my $unexpanded (@$rules) {
            my $action   = $unexpanded->{action};
            my $prt_list = proto_descr(expand_protocols($unexpanded->{prt}, 
                                                        "rule in $context"));
            my $has_user = $unexpanded->{has_user};
        
            for my $element ($foreach ? @$user : ($user)) {
                my $src_dst_list_pairs = 
                    normalize_src_dst_list($unexpanded, $element, $context);
                for my $src_dst_list (@$src_dst_list_pairs) {
                    my ($src_list, $dst_list) = @$src_dst_list;

                    # Artificially take one of 'src' and 'dst' as user
                    # for case like
                    # src = user; dst = any:[user];
                    if ($has_user eq 'both' and
                        not aref_eq($src_list, $dst_list)) 
                    {
                        if ($seen_as_user->($src_list)) {
                            $has_user = 'src';
                        }
                        elsif($seen_as_user->($dst_list)) {
                            $has_user = 'dst';
                        }
                        elsif(@$src_list >= @$dst_list) {
                            $has_user = 'src';
                        }
                        else {                                
                            $has_user = 'dst';
                        }
                    }
                    my $rule = {
                        action   => $action,
                        prt      => $prt_list,
                        has_user => $has_user,
                    };
                    if ($has_user eq 'src') {
                        $rule->{dst} = $dst_list;
                    }
                    elsif ($has_user eq 'dst') {
                        $rule->{src} = $src_list;
                    }

                    my $user_list = $has_user eq 'dst' ? $dst_list : $src_list;
                    my $user_key = join(',', map { $_->{name} } @$user_list);
                    push @{ $key2rules{$user_key} }, $rule;
                    $key2user{$user_key} = $user_list;
                }
            }
        }

        # 'user' has different value for some rules
        # and we get multiple services with identical name.
        my $is_split = (1 < keys %key2rules);

        for my $user_key (keys %key2rules) {
            my $user_list = $key2user{$user_key};
            my $rules     = $key2rules{$user_key};
            my $new_service = { %$service,
                                user  => $user_list,
                                rules => $rules, 
            };
            $new_service->{is_split} = 1 if $is_split;
            push @normalized_services, $new_service;
        }
    }
}
      
sub setup_service_info {
    progress("Setup service info");
    
    for my $service (@normalized_services) {
        my $users = $service->{user};

        # Non 'user' objects.
        my @objects;

        # Check, if service contains a coupling rule with only "user" elements.
        my $is_coupling = 0;

        for my $rule (@{ $service->{rules} }) {
            my $has_user = $rule->{has_user};
            if ($has_user eq 'both') {
                $is_coupling = 1;
                next;
            }
            for my $what (qw(src dst)) {

                next if $what eq $has_user;
                my $all = $rule->{$what};
                push(@objects, @$all);
            }
        }

        @objects = unique(@objects);

        if ($is_coupling) {
            @objects = unique(@objects, @$users);
            $users = [];
        }

        # Store referenced objects for later use during export.
        @all_objects{@objects, @$users} = (@objects, @$users);

        # Input: owner objects, output: owner names
        my $owners = owners_for_objects(\@objects); 
        if ($service->{sub_owner}) {
            $service->{sub_owner} = $service->{sub_owner}->{name};
        }
        $service->{owners} = $owners;
        $service->{part_owners} = part_owners_for_objects(\@objects);
        $service->{extended_owners} = extended_owners_for_objects(\@objects);

        my $uowners = owners_for_objects($users);
        $service->{uowners} = $uowners;
        $service->{part_uowners} = part_owners_for_objects($users);
        $service->{extended_uowners} = extended_owners_for_objects($users);

        # Für Übergangszeit aus aktueller Benutzung bestimmen.
        $service->{visible} ||= find_visibility($owners, $uowners);
        $service->{visible} and $service->{visible} =~ s/\*$/.*/;
    }
}

######################################################################
# Fill attribute part_owners at objects which contain objects
# belonging to other owners.
######################################################################

sub setup_part_owners {
    progress("Setup part owners");

    # Handle hosts of network.
    # Don't handle interfaces here, because
    # - unmanaged interface doesn't have owner and
    # - managed interface isn't part of network.
    for my $network (values %networks) {
        $network->{disabled} and next;
        my $net_owner = $network->{owner} || '';
        for my $host (@{ $network->{hosts} }) {
            my $owner = $host->{owner} or next;
            if ($owner ne $net_owner) {
                $network->{part_owners}->{$owner} = $owner;
#               debug "$network->{name} : $owner->{name}";
            }
        }
    }

    # Add owner and part_owner of network to enclosing aggregates,
    # networks and zone.
    for my $network (values %networks) {
        $network->{disabled} and next;
        my @owners;
        if (my $hash = $network->{part_owners}) {
            @owners = values %$hash;
        }
        if (my $net_owner = $network->{owner}) {
            push @owners, $net_owner;
        }
        my $add_part_owner = sub {
            my($obj) = @_;
            my $obj_owner = $obj->{owner} || '';
            for my $owner (@owners) {
                if ($owner ne $obj_owner) {
                    $obj->{part_owners}->{$owner} = $owner;
#                   debug "$obj->{name} : $owner->{name}";
                }
            }
        };            
        my $up = $network->{up};
        while($up) {
            $add_part_owner->($up);
            $up = $up->{up};
        }
        $add_part_owner->($network->{zone});   
    }

    # Substitute hash by array in attribute {part_owners}.
    for my $network (values %networks) {
        $network->{disabled} and next;
        if (my $hash = $network->{part_owners}) {
            $network->{part_owners} = [ values %$hash ];
        }
    }
    for my $zone (@all_zones) {
        if (my $hash = $zone->{part_owners}) {
            $zone->{part_owners} = [ values %$hash ];
        }
        for my $obj (values %{ $zone->{ipmask2aggregate} }) {
            if (my $hash = $obj->{part_owners}) {

                # Ignore supernet which is both, network and member of
                # ipmask2aggregate.
                next if !$obj->{is_aggregate};
                $obj->{part_owners} = [ values %$hash ];
            }
        }
    }
    return;
}

my $master_owner;

sub find_master_owner {
    for my $owner (values %owners) {
        if ($owner->{show_all}) {
            my $name = $owner->{name};
            $master_owner = $name;
            progress("Found master owner: $name");
            last;
        }
    }
    return;
}

######################################################################
# Export no-NAT-set
# - relate each network to its owner and part_owners
# - build a no_nat_set for each owner, where own networks aren't translated
######################################################################

sub export_no_nat_set {
    progress("Export no-NAT-sets");
    my %owner2net;
    for my $network (values %networks) {
        $network->{disabled} and next;
        for my $owner_name 
            (owner_for_object($network), 
             part_owners_for_object($network),
             extended_owners_for_object($network),
            )
        {
            $owner2net{$owner_name}->{$network} = $network;
        }
    }
    my %owner2no_nat_set;
    my %all_nat_tags;
    $owner2net{$_} ||= {} for keys %owners;
    for my $owner_name (sort keys %owner2net) {
        my %nat_domains;
        for my $network (values %{ $owner2net{$owner_name} }) {
            my $nat_domain = $network->{nat_domain};
            $nat_domains{$nat_domain} = $nat_domain;
        }
        my @nat_domains = values %nat_domains;
        if (not @nat_domains) {

            # Special value 'undef' marks owner without any networks.
            # This will be changed to all_nat_tags below.
            $owner2no_nat_set{$owner_name} = undef;
            next;
        }
#       if ((my $count = @nat_domains) > 1) {
#           print "$owner_name has $count nat_domains\n";
#           for my $network (values %{ $owner2net{$owner_name} }) {
#               my $d = $network->{nat_domain};
#               print " - $d->{name}: $network->{name}\n";
#           }
#       }

        # Build union of no_nat_sets
        my $no_nat_set = [ sort(unique(map { keys(%{ $_->{no_nat_set} }) } 
                                           @nat_domains)) ];
#       debug "$owner_name: ", join(',', sort @$no_nat_set);
        $owner2no_nat_set{$owner_name} = $no_nat_set;
        @all_nat_tags{@$no_nat_set} = @$no_nat_set;
    }
    my @all_nat_tags = sort values %all_nat_tags;
    for my $owner_name (keys %owner2no_nat_set) {
        my $no_nat_set = $owner2no_nat_set{$owner_name} || \@all_nat_tags;
        create_dirs("owner/$owner_name");
        export("owner/$owner_name/no_nat_set", $no_nat_set);
    }
    return;
}

####################################################################
# Export hosts, networks and zones (represented by aggregate 0/0) for
# each owner and sub_owner.
####################################################################

# {networks} only contains toplevel networks.
# Add subnets recursively.
sub add_subnetworks {
    my ($networks) = @_;
    my @sub_networks;
    for my $network (@$networks) {
        if (my $sub = $network->{networks}) {
            push @sub_networks, @{ add_subnetworks($sub) };
        }
    }
    return @sub_networks ? [ @$networks, @sub_networks ] : $networks;
}

sub export_assets {
    progress("Export assets");
    my %result;

    my $export_networks = sub {
        my ($networks, $owner, $own_zone) = @_;
        my %sub_result;
        for my $net (@$networks) {
            next if $net->{disabled};
            next if $net->{ip} eq 'tunnel';
            $all_objects{$net} = $net;
            next if $net->{loopback};
            my $net_name = $net->{name};

            # Export hosts and interfaces.
            my @childs = (@{ $net->{hosts} }, @{ $net->{interfaces} });

            # Show only own childs in foreign network.
            if (!$own_zone) {
                my $net_owner = owner_for_object($net) || '';
                if ($net_owner ne $owner) {
                    @childs = 
                        grep({ my $o = owner_for_object($_); $o and $o eq $owner } 
                             @childs);
                }
            }

            @all_objects{@childs} = @childs;
            @childs = sort map { $_->{name} } @childs;
            $sub_result{$net_name} = \@childs;
        }
        return \%sub_result;
    };

    # Different zones can use the same name from ipmask2aggregate
    # '0/0' if they belong to the same zone_cluster. 
    # Hence augment existing hash.
    my $add_networks_hash = sub {
        my ($owner, $name, $hash) = @_;
        @{ $result{$owner}->{anys}->{$name}->{networks} }{ keys %$hash } =
            values %$hash;
    };

    for my $zone (@all_zones) {
        next if $zone->{disabled};
        next if $zone->{loopback};

        # All aggregates can be used in rules.
        for my $aggregate (values %{ $zone->{ipmask2aggregate} }) {
            $all_objects{$aggregate} = $aggregate;
        }

        # Ignore empty zone with only tunnel or unnumbered networks.
        next if not @{ $zone->{networks} };

        # Zone with network 0/0 doesn't have an aggregate 0/0.
        my $any = $zone->{ipmask2aggregate}->{'0/0'};
        my $zone_name = $any ? $any->{name} : $zone->{name};
#        debug "$zone_name";
        my $networks = add_subnetworks($zone->{networks});
        for my $owner (owner_for_object($zone), 
                       extended_owners_for_object($zone))
        {
#            debug "- $_->{name}" for @$own_networks;
            $add_networks_hash->(
                $owner, 
                $zone_name, 
                $export_networks->($networks, $owner, 1));
        }
        for my $owner (part_owners_for_object($zone)) {

            # Show only own or part_owned networks in foreign zone.
            my $own_networks = 
                [ grep 
                  { grep({ $owner eq $_ } 
                         owner_for_object($_), part_owners_for_object($_)) }
                  @$networks ];
#            debug "- $_->{name}" for @$own_networks;
            $add_networks_hash->(
                $owner, 
                $zone_name, 
                $export_networks->($own_networks, $owner, 0));
        }
        if ($master_owner) {
            $add_networks_hash->(
                $master_owner, 
                $zone_name,
                $export_networks->($networks, $master_owner, 1));
        }
    }

    $result{$_} ||= {} for keys %owners;
    for my $owner (keys %result) {
        my $hash = $result{$owner};
        create_dirs("owner/$owner");
        export("owner/$owner/assets", $hash);
    }
    return;
}

####################################################################
# Services, rules, users
####################################################################

# Set attribute {visible_owner_hash} once for all objects.
# When creating user and service lists for each owner,
# we need to lookup, if an object should be visible by this owner.
sub set_visible_owner_hash {
    for my $object (values %all_objects) {
        my @owner_list = (
            owner_for_object($object),
            part_owners_for_object($object),
            extended_owners_for_object($object)
        );
        @{$object->{visible_owner_hash}}{@owner_list} = @owner_list;
    }
}

# Calculate unique id for set of rules.
# Take first 8 characters of base64 encoded SHA1 hash.
# This gives 8x6=48 bits.
# Collisions would occur with probability of 0.5 for 2^24 different ids.
# We should be extremely safe for up to 2^14 different ids.
sub calc_rules_key {
    my ($rules) = @_;
    my $text = to_json($rules, {canonical => 1});
    my $digest = sha1_base64($text);
    my $result = substr($digest, 0, 8);
    $result =~ tr/+\//-_/;
    return $result;
}

sub export_services {
    progress("Export services");
    my %shash;
    my %owner2type2shash;
    for my $service (sort by_name @normalized_services) {
        next if $service->{disabled};

        my @rules = map {
            { 
                action => $_->{action},
                has_user => $_->{has_user},
                src => [ sort map { $_->{name} } @{ $_->{src} } ],
                dst => [ sort map { $_->{name} } @{ $_->{dst} } ],
                prt => $_->{prt},
            }
        } @{ $service->{rules} };

        # Add extension to make name of split service unique.
        if ($service->{is_split}) {
            my $rules_key = calc_rules_key(\@rules);
            $service->{name} .= "($rules_key)";
        }

        if ($master_owner) {
            $owner2type2shash{$master_owner}->{owner}->{$service} = $service;
        }
        for my $owner ($service->{sub_owner} || (),
                       @{ $service->{owners} }, 
                       @{ $service->{part_owners} },
                       @{ $service->{extended_owners} })
        {
            $owner2type2shash{$owner}->{owner}->{$service} = $service;
        }          
        for my $owner (@{ $service->{uowners} }, 
                       @{ $service->{part_uowners} },
                       @{ $service->{extended_uowners} }) 
        {
            next if $owner2type2shash{$owner}->{owner}->{$service};
            $owner2type2shash{$owner}->{user}->{$service} = $service;
        }
        if (my $visible = $service->{visible}) {
            for my $owner (keys %owners) {
                my $hash = $owner2type2shash{$owner} ||= {};
                next if $hash->{owner}->{$service};
                next if $hash->{user}->{$service};
                if ($owner =~ /^$visible/) {
                    $hash->{visible}->{$service} = $service;
                }
            }
        }

        # Show artificial owner :unknown if owner is unknown.
        my $exported_owners = $service->{owners};
        push @$exported_owners, ':unknown' if !@$exported_owners;
        my $details = {
            description => $service->{description},
            owner => $exported_owners,
        };
        if ($service->{sub_owner}) {
            $details->{sub_owner} = $service->{sub_owner};
        }

# Currently not used in backend.
#       if (@{ $service->{part_owners} }) {
#           $details->{part_owners} = $service->{part_owners};
#       }

        (my $sname = $service->{name}) =~ s/^\w+://;
        $shash{$sname} = { details => $details, rules => \@rules };
    }
    export("services", \%shash);
    return \%owner2type2shash;
}

sub export_users_and_service_lists {
    my ($owner2type2shash) = @_;

    progress("Export users and service_lists");
    set_visible_owner_hash();

    # Create file even for owner having no service at all.
    $owner2type2shash->{$_} ||= {} for keys %owners;
    for my $owner (sort keys %$owner2type2shash) {
        my $type2shash = $owner2type2shash->{$owner};
        my %type2snames;
        my %service2users;
        for my $type (qw(owner user visible)) {
            my @snames;
            for my $service (values %{ $type2shash->{$type} }) { 
                (my $sname = $service->{name}) =~ s/^\w+://;
                push @snames, $sname;
                next if $type eq 'visible';
                my @users;
                if ($type eq 'owner') {
                    @users = @{ $service->{user} };
                }
                elsif ($type eq 'user') {
                    @users = grep({ $_->{visible_owner_hash}->{$owner} }
                                  @{ $service->{user} });
                }
                @users = sort map { $_->{name} } @users;
                $service2users{$sname} = \@users;
            }
            $type2snames{$type} = [ sort @snames ];
        }
        create_dirs("owner/$owner");
        export("owner/$owner/service_lists", \%type2snames);
        export("owner/$owner/users", \%service2users);
    }
    return;
}

####################################################################
# Export all objects referenced by rules, users and assets.
####################################################################

sub zone_and_subnet {
    my ($obj) = @_;
    if (is_interface($obj) && $obj->{loopback}) {
        $obj = $obj->{network};
    }
    is_network $obj or return ();
    my $zone = $obj->{zone};
    if ($obj->{is_aggregate}) {
        if (my $cluster = $zone->{zone_cluster}) {

            # Get derterministic zone for multiple aggregates with
            # identical name from zone cluster.
            ($zone) = @$cluster;
        }
    }
    my $any = $zone->{ipmask2aggregate}->{'0/0'};
    my $zone_name = $any ? $any->{name} : $zone->{name};

    # Netspoc-Web only needs info about subnets in other zone.
    # Attribute name is different for historic reasons.
    my $is_supernet = $obj->{has_other_subnet};
#    debug "$obj->{name} $zone_name";
    return (zone => $zone_name, $is_supernet ? (is_supernet => 1) : () );
}

sub export_objects {
    progress("Export objects");
    my %objects = map { 
        $_->{name} => { 


            # Add key 'ip' and optionally key 'nat'.
            ip_nat_for_object($_),

            # Add key 'zone' for network and aggregate.
            # Optionally add key 'is_supernet' for network and aggregate.
            zone_and_subnet($_),

            owner => scalar owner_for_object($_),
        } 
    } values %all_objects;
    export("objects", \%objects);
    return;
}

####################################################################
# find Email -> Owner
####################################################################

sub export_owners {
    progress("Export owners");
    my %email2owners;
    for my $name ( keys %owners ) {
        my $owner = $owners{$name};
        my @emails;
        my @watchers;
        my @e_owners;
        create_dirs("owner/$name");
        for my $email ( @{ $owner->{admins} } ) {
            $email2owners{$email}->{$name} = $name;
            push @emails, $email;
        }
        for my $email ( @{ $owner->{watchers} } ) {

            # Watchers are allowed to login, but aren't shown as owner.
            $email2owners{$email}->{$name} = $name;
            push @watchers, $email;
        }

        # Handle extending owners.
        my $extended = $owner->{extended_by} || [];

        # Add master owner to owners not already extended_by, 
        # i.e. sub_owner.
        if ($master_owner && $name ne $master_owner) {
            my $m_owner = $owners{$master_owner};
            if (!grep { $_ eq $m_owner } @$extended) {
                push @$extended, $m_owner;
            }
        }
        for my $e_owner (@$extended) {

            # Allow both, admins and watchers to look at owner.
            for my $email ( @{ $e_owner->{admins} }, @{ $e_owner->{watchers} } ) {
                $email2owners{$email}->{$name} = $name;
            }
            my $e_name = $e_owner->{name};
            push @e_owners, $e_name;
        }

        export("owner/$name/emails", 
               [ map { { email => $_ } } sort @emails ]);
        export("owner/$name/watchers", 
               [ map { { email => $_ } } sort @watchers ]);
        export("owner/$name/extended_by", 
               [ map { { name => $_ } } sort @e_owners ]);
    }
        
    # Substitute hash by array.
    $_ = [ sort values(%$_) ] for values %email2owners;

    export("email", \%email2owners);

    my %owner2alias;
    for my $name (keys %owners) {
        my $owner = $owners{$name};
        if (my $alias = $owner->{alias}) {
            $owner2alias{$name} = $alias;
        }
    }
    export('owner2alias', \%owner2alias);
    return;
}

sub copy_policy_file {
    if ( -f $policy_file) {
        system("find $out_dir -type f -exec touch -r $policy_file {} \\;");
        system("cp -pf $policy_file $out_dir") == 0 or
            abort "Can't copy $policy_file";
    }
    return;
}

####################################################################
# Initialize Netspoc data
####################################################################
$config = 
    combine_config({time_stamps => 1, max_errors => 9999, verbose => !$quiet});
init_global_vars();
read_file_or_dir($netspoc_data);
order_protocols();
link_topology();
mark_disabled();
set_zone();
setpath();
distribute_nat_info();
find_subnets_in_zone();
normalize_services_for_export();
propagate_owners();
find_subnets_in_nat_domain();
adapt_owner_names();
setup_zones();
setup_part_owners();
setup_service_info();
find_master_owner();

####################################################################
# Export data
####################################################################
create_dirs('');
export_owners();
export_assets();
my $owner2type2shash = export_services();
export_users_and_service_lists($owner2type2shash);
export_objects();
export_no_nat_set();
copy_policy_file();
progress("Ready");
